импорт ИнтерфейсОбменаСлужебный

метод ПримерМетодаGET(Запрос: HttpСервисЗапрос)

	// Быстрое формирование заголовков ответа
    Запрос.Ответ.Заголовки.УстановитьЗаголовки(новый ЗаголовкиHttp(ИнтерфейсОбменаСлужебный.ЗаголовкиОтвета() ) )
    
    попытка 
		// Чтение идентификатора из параметров
        пер ИдСтрокой: Строка? = Неопределено
        если Запрос.Параметры.Содержит("id")
            ИдСтрокой = Запрос.Параметры.ПолучитьПервый("id")
        ;
		// Считываем данные в виде массива соответствий
        знч МассивДанных = ИнтерфейсОбменаЛогикаОбработки.ПолучитьДанныеОбъектов(ИдСтрокой)
        // Формируем JSON ответ
		пер ТекстОтвета: Строка
        если ИдСтрокой != Неопределено
            ТекстОтвета = СериализацияJson.ЗаписатьОбъект(МассивДанных.Первый() )
        иначе
            ТекстОтвета = СериализацияJson.ЗаписатьОбъект(МассивДанных)
        ;    

        Запрос.Ответ.УстановитьТело(ТекстОтвета)
        
    поймать Исключение: Исключение
    
        Запрос.Ответ.УстановитьКодСтатуса(400)
        Запрос.Ответ.УстановитьТело(Исключение.Описание)
        
    ;
;

метод ПримерМетодаPOST(Запрос: HttpСервисЗапрос)
    
	// Быстрое формирование заголовков ответа
    Запрос.Ответ.Заголовки.УстановитьЗаголовки(новый ЗаголовкиHttp(ИнтерфейсОбменаСлужебный.ЗаголовкиОтвета() ) )
    
    попытка 
    
        // Чтение идентификатора из параметров
        пер ИдСтрокой: Строка? = Неопределено
        если Запрос.Параметры.Содержит("id")
            ИдСтрокой = Запрос.Параметры.ПолучитьПервый("id")
        ;
        если ИдСтрокой == Неопределено
            выбросить новый ИсключениеВалидации("Не указан идентификатор")
        ;
		// Считываем данные и передаем в обработку. Как решение, можно передавать текст запроса
		// а потом загружать содержание в структуру
        знч СтруктураДанных = СериализацияJson.ПрочитатьСоответствие(Запрос.Тело)  
        ИнтерфейсОбменаЛогикаОбработки.ОбновитьОбъект(ИдСтрокой, СтруктураДанных)  
        знч ОбъектОтвета = {"Результат": "Успех"} 
        знч ТекстОтвета = СериализацияJson.ЗаписатьОбъект(ОбъектОтвета)
        Запрос.Ответ.УстановитьТело(ТекстОтвета)
        
    поймать Исключение: Исключение
        Запрос.Ответ.УстановитьКодСтатуса(400)
        Запрос.Ответ.УстановитьТело(Исключение.Описание)    
    ;
;

метод ПримерМетодаPUT(Запрос: HttpСервисЗапрос)
    
	// Быстрое формирование заголовков ответа
    Запрос.Ответ.Заголовки.УстановитьЗаголовки(новый ЗаголовкиHttp(ИнтерфейсОбменаСлужебный.ЗаголовкиОтвета() ) )
    
    попытка 
    
        // Считываем данные и передаем в обработку. Как решение, можно передавать текст запроса
		// а потом загружать содержание в структуру
        знч СтруктураДанных = СериализацияJson.ПрочитатьСоответствие(Запрос.Тело)  
        знч РезультатОбработки = ИнтерфейсОбменаЛогикаОбработки.СоздатьОбъект(СтруктураДанных)  
        знч ОбъектОтвета = {"Ссылка": РезультатОбработки} 
        знч ТекстОтвета = СериализацияJson.ЗаписатьОбъект(ОбъектОтвета)
        Запрос.Ответ.УстановитьТело(ТекстОтвета)
        
    поймать Исключение: Исключение
        Запрос.Ответ.УстановитьКодСтатуса(400)
        Запрос.Ответ.УстановитьТело(Исключение.Описание)    
    ;
;
